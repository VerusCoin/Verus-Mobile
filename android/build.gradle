// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "31.0.0"
        minSdkVersion = 27
        compileSdkVersion = 33
        targetSdkVersion = 33

        if (System.properties['os.arch'] == "aarch64") {
            // For M1 Users we need to use the NDK 24 which added support for aarch64
            ndkVersion = "24.0.8215888"
        } else {
            // Otherwise we default to the side-by-side NDK version from AGP.
            ndkVersion = "21.4.7075529"
        }

        kotlinVersion = '1.8.22'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
        google()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.3.1")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("de.undercouch:gradle-download-task:4.1.2")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        //jcenter()
        gradlePluginPortal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        google()
        //maven { url 'https://www.jitpack.io' }
    }
}

subprojects {
  project.configurations.all {
     resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.android.support'
              && !details.requested.name.contains('multidex') ) {
           details.useVersion "26.1.0"
        }

        if (details.requested.group == 'junit' && details.requested.name == 'junit') {
            details.useVersion '4.13.1'
            details.because 'https://snyk.io/vuln/SNYK-JAVA-JUNIT-1017047'
        }

        if (details.requested.group == 'org.robolectric' && details.requested.name == 'robolectric') {
            details.useVersion '4.4'
            details.because 'https://snyk.io/vuln/SNYK-JAVA-ORGBOUNCYCASTLE-32340'
        }

        if (details.requested.group == 'org.bouncycastle' && details.requested.name == 'bcprov-jdk15on') {
            details.useVersion '1.69'
            details.because 'https://snyk.io/vuln/SNYK-JAVA-ORGBOUNCYCASTLE-32340'
        }

        if (details.requested.group == 'com.drewnoakes' && details.requested.name == 'metadata-extractor') {
            details.useVersion '2.13.0'
            details.because 'https://snyk.io/vuln/SNYK-JAVA-COMDREWNOAKES-455419'
        }

        if (details.requested.group == 'org.powermock' && details.requested.name == 'powermock-classloading-xstream') {
            details.useVersion '2.0.0'
            details.because 'https://snyk.io/vuln/SNYK-JAVA-COMTHOUGHTWORKSXSTREAM-31394'
        }

        if (details.requested.group == 'org.apache.httpcomponents' && details.requested.name == 'httpclient') {
            details.useVersion '4.5.13'
            details.because 'https://snyk.io/vuln/SNYK-JAVA-ORGAPACHEHTTPCOMPONENTS-31517'
        }

        if (details.requested.group == 'org.apache.commons' && details.requested.name == 'commons-compress') {
            details.useVersion '1.19'
            details.because 'https://snyk.io/vuln/SNYK-JAVA-ORGAPACHECOMMONS-32473'
        }

        if (details.requested.group == 'commons-codec' && details.requested.name == 'commons-codec') {
            details.useVersion '1.13'
            details.because 'https://snyk.io/vuln/SNYK-JAVA-COMMONSCODEC-561518'
        }

        if (details.requested.group == 'com.google.guava' && details.requested.name == 'guava') {
            details.useVersion '30.0-jre'
            details.because 'https://snyk.io/vuln/SNYK-JAVA-COMGOOGLEGUAVA-1015415'
        }

        if (details.requested.group == 'com.google.android.gms' && details.requested.name == 'play-services-basement') {
            details.useVersion '18.0.2'
            details.because 'https://security.snyk.io/vuln/SNYK-JAVA-COMGOOGLEANDROIDGMS-2987459'
        }

        if (details.requested.group == 'com.google.protobuf' && details.requested.name == 'protobuf-java') {
            details.useVersion '3.19.6'
            details.because 'https://security.snyk.io/vuln/SNYK-JAVA-COMGOOGLEPROTOBUF-2331703'
        }

        if (details.requested.group == 'org.apache.commons' && details.requested.name == 'commons-compress') {
            details.useVersion '1.21'
            details.because 'https://security.snyk.io/vuln/SNYK-JAVA-ORGAPACHECOMMONS-1316641'
        }

        if (details.requested.group == 'org.jetbrains.kotlin' && details.requested.name == 'kotlin-stdlib') {
            details.useVersion '1.6.0'
            details.because 'https://security.snyk.io/vuln/SNYK-JAVA-ORGJETBRAINSKOTLIN-2628385'
        }

        if (details.requested.group == 'com.google.code.gson' && details.requested.name == 'gson') {
            details.useVersion '2.8.9'
            details.because 'https://security.snyk.io/vuln/SNYK-JAVA-COMGOOGLECODEGSON-1730327'
        }
     }
  }
}
